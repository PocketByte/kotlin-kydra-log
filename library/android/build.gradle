apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.android_sdk_compile
    buildToolsVersion project.android_build_tool_version

    defaultConfig {
        minSdkVersion project.android_sdk_min
        targetSdkVersion project.android_sdk_target
        versionCode 1
        versionName project.version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api project(':hydra-log:hydra-log-jvm')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

//==================================================================================================
// Publication
//==================================================================================================

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = "hydra-log-android"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId project.group
                artifactId 'hydra-log-android'
            }
            withXml {
                def root = asNode()

                def dependencies = root.get('dependencies') ?: root.appendNode('dependencies')

                // Add common dependency
                def commonDependency = dependencies.appendNode('dependency')
                commonDependency.appendNode('groupId', project.group)
                commonDependency.appendNode('artifactId', 'hydra-log-jvm')
                commonDependency.appendNode('version', project.version)
                commonDependency.appendNode('scope', 'runtime')
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.withType(Javadoc) {
    // FIXME generate java docs
    enabled = false
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintray.publish.user') ? project.property('bintray.publish.user') : ''
    key = project.hasProperty('bintray.publish.apikey') ? project.property('bintray.publish.apikey') : ''
    publish = true
    override = false

    pkg {
        repo = "maven"
        name = "hydra-log-android"
        userOrg = 'pocketbyte'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/PocketByte/'
        version {
            name = project.version
            released  = new Date()
        }
    }
    configurations = ['archives']
}