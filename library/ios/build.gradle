apply plugin: 'org.jetbrains.kotlin.platform.native'

components.main {
    targets = ['macos_x64', 'ios_arm32', 'ios_arm64', 'ios_x64']

    outputKinds = [KLIBRARY]
    publishSources = true
    publishJavadoc = true
}

dependencies {
    expectedBy project(':hydra-log:hydra-log-common')
}


//==================================================================================================
// Publication
//==================================================================================================

apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

bintrayUpload.doFirst {
    publications = []

    def pubs = project.extensions.getByName("publishing").getPublications()

    for(pub in pubs) {
        publications += pub.name
        println("groupId ${pub.groupId}, artifactId ${pub.artifactId}")
    }
}

bintray {
    user = project.hasProperty('bintray.publish.user') ? project.property('bintray.publish.user') : ''
    key = project.hasProperty('bintray.publish.apikey') ? project.property('bintray.publish.apikey') : ''
    publish = true
    override = false

    pkg {
        repo = "maven"
        name = "hydra-log-ios"
        userOrg = 'pocketbyte'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/PocketByte/'
        version {
            name = project.version
            released  = new Date()
        }
    }
}

afterEvaluate {
    project.publishing.publications.forEach { publication ->
        publication.pom.withXml {
            def root = asNode()
            def dependencies = root.get('dependencies') ?: root.appendNode('dependencies')

            // Add common dependency
            def commonDependency = dependencies.appendNode('dependency')
            commonDependency.appendNode('groupId', project.group)
            commonDependency.appendNode('artifactId', 'hydra-log-common')
            commonDependency.appendNode('version', project.version)
            commonDependency.appendNode('scope', 'runtime')
        }
    }
}