apply plugin: "com.android.library"
apply plugin: "kotlin-multiplatform"

version = project.hydra_log_version
group = "ru.pocketbyte.hydra"

android {
    compileSdkVersion project.android_sdk_compile
    buildToolsVersion project.android_build_tool_version

    defaultConfig {
        minSdkVersion project.android_sdk_min
        targetSdkVersion project.android_sdk_target
        versionCode 1
        versionName project.version
    }
    buildTypes {
        release { }
        debug { }
    }
}

kotlin {
    android {
        publishLibraryVariants("release", "debug")
    }

    targets {
        jvm("jvm")
        android("android")

        js("js")

        macosX64("macosX64") // macOS required
        iosX64("iosX64")     // macOS required
        iosArm64("iosArm64") // macOS required
        iosArm32("iosArm32") // macOS required

        watchosArm32("watchosArm32") // macOS required
        watchosArm64("watchosArm64") // macOS required
        watchosX86("watchosX86")     // macOS required

        tvosArm64("tvosArm64") // macOS required
        tvosX64("tvosX64")     // macOS required

        linuxX64("linuxX64")
        linuxArm64("linuxArm64")
        linuxArm32Hfp("linuxArm32Hfp")

        linuxMips32("linuxMips32")     // Linux required
        linuxMipsel32("linuxMipsel32") // Linux required

        androidNativeArm32("androidNativeArm32")
        androidNativeArm64("androidNativeArm64")
        androidNativeX64("androidNativeX64")
        androidNativeX86("androidNativeX86")

        mingwX64("mingwX64") // Windows required
        mingwX86("mingwX86") // Windows required

        wasm32("wasm32")
    }

    sourceSets {
        // =================================
        // Main Source Sets
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
        }

        commonTest {
            dependsOn commonMain
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test"
                api "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }

        // =================================
        // JVM based Source Sets
        jvmCommonMain {
            dependsOn commonMain
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }

        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }

        androidMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            }
        }

        configure([jvmMain, androidMain]) {
            dependsOn jvmCommonMain
        }

        // Tests
        jvmCommonTest {
            dependsOn jvmCommonMain
            dependsOn commonTest
        }

        configure([jvmTest]) {
            dependsOn jvmCommonTest
            dependsOn jvmMain
        }

        configure([androidTest]) {
            dependsOn jvmCommonTest
            dependsOn androidMain
        }

        // =================================
        // JS Source Sets
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                api "org.jetbrains.kotlin:kotlin-stdlib-js"
            }
        }

        jsTest {
            dependsOn jsMain
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test"
                api "org.jetbrains.kotlin:kotlin-test-js"
            }
        }

        // =================================
        // Native based Source Sets
        nativeCommonMain { dependsOn commonMain }
        nativeMain {}
        appleMain {}
        androidNativeMain {}
        mingwMain {}

        configure([nativeMain, appleMain, androidNativeMain, mingwMain]) {
            dependsOn nativeCommonMain
        }
        
        configure([
                linuxX64Main, linuxArm64Main, linuxArm32HfpMain,
                linuxMips32Main, linuxMipsel32Main
        ]) {
            dependsOn nativeMain
        }

        configure([
                androidNativeArm32Main, androidNativeArm64Main,
                androidNativeX64Main, androidNativeX86Main
        ]) {
            dependsOn androidNativeMain
        }

        configure([
                macosX64Main,
                iosX64Main, iosArm64Main, iosArm32Main,
                watchosArm32Main, watchosArm64Main, watchosX86Main,
                tvosArm64Main, tvosX64Main
        ]) {
            dependsOn appleMain
        }

        configure([mingwX64Main, mingwX86Main]) {
            dependsOn mingwMain
        }

        configure([wasm32Main]) {
            dependsOn commonMain
        }

        //Tests

        nativeCommonTest {
            dependsOn nativeCommonMain
            dependsOn commonTest
        }
        nativeTest { dependsOn nativeMain }
        appleTest { dependsOn appleMain }
        androidNativeTest { dependsOn androidNativeMain }
        mingwTest { dependsOn mingwMain }

        configure([nativeTest, appleTest, androidNativeTest, mingwTest]) {
            dependsOn nativeCommonTest
        }

        configure([
                linuxX64Test, linuxArm64Test, linuxArm32HfpTest,
                linuxMips32Test, linuxMipsel32Test
        ]) {
            dependsOn nativeTest
        }

        configure([
                androidNativeArm32Test, androidNativeArm64Test,
                androidNativeX64Test, androidNativeX86Test
        ]) {
            dependsOn androidNativeTest
        }

        configure([
                macosX64Test,
                iosX64Test, iosArm64Test, iosArm32Test,
                watchosArm32Test, watchosArm64Test, watchosX86Test,
                tvosArm64Test, tvosX64Test
        ]) {
            dependsOn appleTest
        }

        configure([mingwX64Test, mingwX86Test]) {
            dependsOn mingwTest
        }

        configure([wasm32Test]) {
            dependsOn commonTest
        }
    }
}

//==================================================================================================
// Publication
//==================================================================================================

apply plugin: "maven-publish"
apply plugin: "com.github.dcendents.android-maven"

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = project.name
        }
    }
    repositories {
        maven {
            url = "https://api.bintray.com/content/pocketbyte/hydra/${project.name}/${project.version}"
            credentials {
                username project.hasProperty('bintray.publish.user') ? project.property('bintray.publish.user') : ''
                password project.hasProperty('bintray.publish.apikey') ? project.property('bintray.publish.apikey') : ''
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId project.group
                artifactId "${project.name}-android"
            }
            withXml {
                def root = asNode()

                def dependencies = root.get('dependencies') ?: root.appendNode('dependencies')

                // Add common dependency
                def commonDependency = dependencies.appendNode('dependency')
                commonDependency.appendNode('groupId', project.group)
                commonDependency.appendNode('artifactId', "${project.name}-jvm")
                commonDependency.appendNode('version', project.version)
                commonDependency.appendNode('scope', 'runtime')
            }
        }
    }
}