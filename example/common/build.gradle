apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'


version = '0.1.1'
group = 'ru.pocketbyte.kydralogexample.common_lib'

android {
    compileSdkVersion AndroidSdk.compile
    buildToolsVersion BuildVersion.androidTool

    defaultConfig {
        minSdkVersion AndroidSdk.min
        targetSdkVersion AndroidSdk.target
        versionCode 1
        versionName project.version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
        fromPreset(presets.js, 'js')

        final def appleIosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(appleIosTarget, 'ios') {
            binaries {
                framework('common_ios') {
                    export("ru.pocketbyte.kydra:kydra-log:$LibraryInfo.version")
                }
            }
        }

        final def appleWatchTarget = System.getenv('SDK_NAME')?.startsWith("watchos") \
                              ? presets.watchosArm64 : presets.watchosX86

        fromPreset(appleWatchTarget, 'watch') {
            binaries {
                framework('common_watch')
            }
        }

        final def appleTvTarget = System.getenv('SDK_NAME')?.startsWith("tvos") \
                              ? presets.tvosArm64 : presets.tvosX64

        fromPreset(appleTvTarget, 'tv') {
            binaries {
                framework('common_tv')
            }
        }
    }

    sourceSets {
        // Main Source Sets
        commonMain {

            kotlin.srcDir('src/main/kotlin')

            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common"
                api "ru.pocketbyte.kydra:kydra-log:$LibraryInfo.version"
            }
        }

        androidMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            }
        }

        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                api "org.jetbrains.kotlin:kotlin-stdlib-js"
            }
        }

        iosMain { }
        watchMain { }
        tvMain { }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}